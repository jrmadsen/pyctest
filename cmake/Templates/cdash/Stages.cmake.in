
if(EXISTS "${CMAKE_CURRENT_LIST_DIR}/PyCTestPreInit.cmake")
    include("${CMAKE_CURRENT_LIST_DIR}/PyCTestPreInit.cmake")
endif(EXISTS "${CMAKE_CURRENT_LIST_DIR}/PyCTestPreInit.cmake")

macro(SET_IF_DEFINED IF_DEF_VAR PREFIX_VAR SET_VAR)
    if(DEFINED "${IF_DEF_VAR}")
        set(${SET_VAR} "${PREFIX_VAR} \"${${IF_DEF_VAR}}\"")
    endif()
endmacro()

if(EXISTS "${CMAKE_CURRENT_LIST_DIR}/Init.cmake")

    if(NOT DEFINED CTEST_TRIGGER)
        set(CTEST_TRIGGER "None" CACHE STRING
            "Delay submit until CTEST_TRIGGER == '(Any|Build|Test|Coverage|MemCheck|None)'")
    endif()

    include("${CMAKE_CURRENT_LIST_DIR}/Init.cmake")
    include_if("${CMAKE_CURRENT_LIST_DIR}/PyCTestPostInit.cmake")

    list(FIND STAGES "Start" DO_START)
    list(FIND STAGES "Update" DO_UPDATE)
    list(FIND STAGES "Configure" DO_CONFIGURE)
    list(FIND STAGES "Build" DO_BUILD)
    list(FIND STAGES "Test" DO_TEST)
    list(FIND STAGES "Coverage" DO_COVERAGE)
    list(FIND STAGES "MemCheck" DO_MEMCHECK)
    list(FIND STAGES "Submit" DO_SUBMIT)
    list(FIND STAGES "${CTEST_TRIGGER}" DO_TRIGGER)

    message(STATUS "")
    message(STATUS "STAGES = ${STAGES}")
    message(STATUS "")

    copy_ctest_config_files()
    ctest_read_custom_files("${CMAKE_CURRENT_LIST_DIR}")

    set_if_defined(CTEST_START      START       _CTEST_START)
    set_if_defined(CTEST_END        END         _CTEST_END)
    set_if_defined(CTEST_STRIDE     STRIDE      _CTEST_STRIDE)
    set_if_defined(CTEST_INCLUDE    INCLUDE     _CTEST_INCLUDE)
    set_if_defined(CTEST_EXCLUDE    EXCLUDE     _CTEST_EXCLUDE)
    set_if_defined(CTEST_INCLUDE_LABEL  INCLUDE_LABEL   _CTEST_INCLUDE_LABEL)
    set_if_defined(CTEST_EXCLUDE_LABEL  EXCLUDE_LABEL   _CTEST_EXCLUDE_LABEL)
    set_if_defined(CTEST_PARALLEL_LEVEL PARALLEL_LEVEL  _CTEST_PARALLEL_LEVEL)
    set_if_defined(CTEST_STOP_TIME      STOP_TIME       _CTEST_STOP_TIME)

    set_if_defined(CTEST_COVERAGE_LABELS LABELS _CTEST_COVERAGE_LABELS)

    #-------------------------------------------------------------------------#
    # Start
    #
    ctest_start(${CTEST_MODEL} TRACK ${CTEST_MODEL} ${CTEST_SOURCE_DIRECTORY} ${CTEST_BINARY_DIRECTORY})
    if("${DO_START}" GREATER -1)
        message(STATUS "")
        message(STATUS "[${CTEST_BUILD_NAME}] Running CTEST_START stage...")
        message(STATUS "")
        set(_CTEST_APPEND )
    else()
        message(STATUS "")
        message(STATUS "[${CTEST_BUILD_NAME}] Skipping CTEST_START stage...")
        message(STATUS "")
        set(_CTEST_APPEND APPEND)
    endif()

    #-------------------------------------------------------------------------#
    # Config
    #
    copy_ctest_config_files()
    ctest_read_custom_files("${CTEST_BINARY_DIRECTORY}")

    #-------------------------------------------------------------------------#
    # Update
    #
    if("${DO_UPDATE}" GREATER -1 AND NOT "${CTEST_UPDATE_COMMAND}" STREQUAL "")
        message(STATUS "")
        message(STATUS "[${CTEST_BUILD_NAME}] Running CTEST_UPDATE stage...")
        message(STATUS "")
        ctest_update(SOURCE "${CTEST_SOURCE_DIRECTORY}"
                    RETURN_VALUE up_ret)
    else()
        message(STATUS "")
        message(STATUS "[${CTEST_BUILD_NAME}] Skipping CTEST_UPDATE stage...")
        message(STATUS "")
    endif()

    #-------------------------------------------------------------------------#
    # Configure
    #
    if("${DO_CONFIGURE}" GREATER -1 AND NOT "${CTEST_CONFIGURE_COMMAND}" STREQUAL "")
        message(STATUS "")
        message(STATUS "[${CTEST_BUILD_NAME}] Running CTEST_CONFIGURE stage...")
        message(STATUS "")
        ctest_configure(BUILD "${CTEST_BINARY_DIRECTORY}"
                    SOURCE ${CTEST_SOURCE_DIRECTORY}
                    ${_CTEST_APPEND}
                    OPTIONS "${CTEST_CONFIGURE_OPTIONS}"
                    RETURN_VALUE config_ret)
    else()
        message(STATUS "")
        message(STATUS "[${CTEST_BUILD_NAME}] Skipping CTEST_CONFIGURE stage...")
        message(STATUS "")
    endif()

    #-------------------------------------------------------------------------#
    # Build
    #
    if("${DO_BUILD}" GREATER -1 AND NOT "${CTEST_BUILD_COMMAND}" STREQUAL "")
        message(STATUS "")
        message(STATUS "[${CTEST_BUILD_NAME}] Running CTEST_BUILD stage...")
        message(STATUS "")
        ctest_build(BUILD "${CTEST_BINARY_DIRECTORY}"
                    ${_CTEST_APPEND}
                    RETURN_VALUE build_ret)
    else()
        message(STATUS "")
        message(STATUS "[${CTEST_BUILD_NAME}] Skipping CTEST_BUILD stage...")
        message(STATUS "")
    endif()

    #-------------------------------------------------------------------------#
    # Test
    #
    if("${DO_TEST}" GREATER -1)
        message(STATUS "")
        message(STATUS "[${CTEST_BUILD_NAME}] Running CTEST_TEST stage...")
        message(STATUS "")
        ctest_test(RETURN_VALUE test_ret
                    ${_CTEST_APPEND}
                    ${_CTEST_START}
                    ${_CTEST_END}
                    ${_CTEST_STRIDE}
                    ${_CTEST_INCLUDE}
                    ${_CTEST_EXCLUDE}
                    ${_CTEST_INCLUDE_LABEL}
                    ${_CTEST_EXCLUDE_LABEL}
                    ${_CTEST_PARALLEL_LEVEL}
                    ${_CTEST_STOP_TIME}
                    SCHEDULE_RANDOM OFF)
    else()
        message(STATUS "")
        message(STATUS "[${CTEST_BUILD_NAME}] Skipping CTEST_TEST stage...")
        message(STATUS "")
    endif()

    #-------------------------------------------------------------------------#
    # Coverage
    #
    if("${DO_COVERAGE}" GREATER -1 AND NOT "${CTEST_COVERAGE_COMMAND}" STREQUAL "")
        message(STATUS "")
        message(STATUS "[${CTEST_BUILD_NAME}] Running CTEST_COVERAGE stage...")
        message(STATUS "")
        execute_process(COMMAND ${CTEST_COVERAGE_COMMAND} ${CTEST_COVERAGE_EXTRA_FLAGS}
            WORKING_DIRECTORY ${CTEST_BINARY_DIRECTORY})
        ctest_coverage(${_CTEST_APPEND}
                    ${_CTEST_COVERAGE_LABELS}
                    RETURN_VALUE cov_ret)
    else()
        message(STATUS "")
        message(STATUS "[${CTEST_BUILD_NAME}] Skipping CTEST_COVERAGE stage...")
        message(STATUS "")
    endif()

    #-------------------------------------------------------------------------#
    # MemCheck
    #
    if("${DO_MEMCHECK}" GREATER -1 AND NOT "${CTEST_MEMORYCHECK_COMMAND}" STREQUAL "")
        message(STATUS "")
        message(STATUS "[${CTEST_BUILD_NAME}] Running CTEST_MEMCHECK stage...")
        message(STATUS "")
        ctest_memcheck(RETURN_VALUE mem_ret
                    ${_CTEST_APPEND}
                    ${_CTEST_START}
                    ${_CTEST_END}
                    ${_CTEST_STRIDE}
                    ${_CTEST_INCLUDE}
                    ${_CTEST_EXCLUDE}
                    ${_CTEST_INCLUDE_LABEL}
                    ${_CTEST_EXCLUDE_LABEL}
                    ${_CTEST_PARALLEL_LEVEL})
    else()
        message(STATUS "")
        message(STATUS "[${CTEST_BUILD_NAME}] Skipping CTEST_MEMCHECK stage...")
        message(STATUS "")
    endif()

    #-------------------------------------------------------------------------#
    # Submit
    #
    if("${DO_SUBMIT}" GREATER -1 OR "${DO_TRIGGER}" GREATER -1)
        message(STATUS "")
        message(STATUS "[${CTEST_BUILD_NAME}] Running CTEST_SUBMIT stage...")
        message(STATUS "")
        submit_command()
    else()
        message(STATUS "")
        message(STATUS "[${CTEST_BUILD_NAME}] Skipping CTEST_SUBMIT stage...")
        message(STATUS "")
    endif()

    message(STATUS "")
    message(STATUS "[${CTEST_BUILD_NAME}] Finished ${CTEST_MODEL} Stages (${STAGES})")
    message(STATUS "")

endif(EXISTS "${CMAKE_CURRENT_LIST_DIR}/Init.cmake")
